/**
  # beautysh

  `beautysh` is a Bash beautifier for the masses.

  âš ï¸ `beautysh` and `shfmt` don't produce the same output,
  notably with indentation of multi-line pipes.
  Use one or the other to prevent their git hooks from conflicting.

  ## ğŸ› ï¸ Tech Stack

  - [beautysh @ GitHub](https://github.com/lovesegfault/beautysh).

  ### ğŸ“¦ Third party tools

  - [fd @ GitHub](https://github.com/sharkdp/fd).

  ## ğŸ™‡ Acknowledgements

  - [Setting priorities @ NixOS Manual](https://nixos.org/manual/nixos/stable/#sec-option-definitions-setting-priorities).
  - [lib.meta.getExe @ Nixpkgs Reference Manual](https://nixos.org/manual/nixpkgs/stable/#function-library-lib.meta.getExe).
*/
{
  config,
  lib,
  pkgs,
  recipes-lib,
  ...
}:
let
  inherit (pkgs) fd;
  inherit (lib.modules) mkIf mkDefault;
  inherit (recipes-lib.modules) mkToolOptions;
  inherit (lib.attrsets) optionalAttrs;

  shellCfg = config.biapy-recipes.shell;
  cfg = shellCfg.beautysh;

  fdCommand = lib.meta.getExe fd;
  beautysh = config.git-hooks.hooks.beautysh.package;
  beautyshCommand = lib.meta.getExe beautysh;
in
{
  options.biapy-recipes.shell.beautysh = mkToolOptions { enable = false; } "beautysh";

  config = mkIf cfg.enable {
    packages = [
      beautysh
      fd
    ];

    devcontainer.settings.customizations.vscode = {
      extensions = [ "mkhl.beautysh" ];
    };

    # https://devenv.sh/git-hooks/
    git-hooks.hooks = optionalAttrs cfg.git-hooks { beautysh.enable = mkDefault true; };

    # https://devenv.sh/tasks/
    tasks = optionalAttrs cfg.tasks {
      "ci:lint:sh:beautysh" = {
        description = "ğŸ” Lint ğŸšshell files with beautysh";
        exec = ''
          cd "''${DEVENV_ROOT}"
          ${fdCommand} '\.(sh|bash|dash|ksh)$' "''${DEVENV_ROOT}" \
            --exec ${beautyshCommand} --tab --check
        '';
      };
      "ci:format:sh:beautysh" = {
        description = "ğŸ¨ Format ğŸšshell files with beautysh";
        exec = ''
          cd "''${DEVENV_ROOT}"
          ${fdCommand} '\.(sh|bash|dash|ksh)$' "''${DEVENV_ROOT}" \
            --exec ${beautyshCommand} --tab
        '';
      };
    };

    biapy.go-task.taskfile.tasks = optionalAttrs cfg.go-task {
      "ci:lint:sh:beautysh" = {
        desc = "ğŸ” Lint ğŸšshell files with beautysh";
        cmds = [ ''fd '\.(sh|bash|dash|ksh)$' "''${DEVENV_ROOT}" --exec beautysh --tab --check'' ];
        requires.vars = [ "DEVENV_ROOT" ];
      };

      "ci:format:sh:beautysh" = {
        aliases = [ "beautysh" ];
        desc = "ğŸ¨ Format ğŸšshell files with beautysh";
        cmds = [ ''fd '\.(sh|bash|dash|ksh)$' "''${DEVENV_ROOT}" --exec beautysh --tab'' ];
        requires.vars = [ "DEVENV_ROOT" ];
      };
    };
  };
}
