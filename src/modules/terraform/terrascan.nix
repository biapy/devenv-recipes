/**
  # Terrascan

  Terrascan is a static code analyzer for Infrastructure as Code.
  It detects compliance and security violations across Infrastructure as Code
  to mitigate risk before provisioning cloud native infrastructure.

  :::warning
  Terrascan is currently unmaintained.
  Consider using Checkov or other alternatives.
  :::

  ## ğŸ§ Features

  ### ğŸ”¨ Tasks

  - `ci:lint:tf:terrascan`: scan IaC configuration with `terrascan`.

  ### ğŸ‘· Commit hooks

  - `terrascan`: scan IaC configuration with `terrascan`,
    disabled by default, as Terrascan is unmaintained.

  ## ğŸ› ï¸ Tech Stack

  - [Terrascan homepage](https://runterrascan.io/).
  - [Terrascan @ GitHub](https://github.com/tenable/terrascan).

  ## ğŸ™‡ Acknowledgements

  - [What is Terrascan? Features, Use Cases & Custom Policies @ spacelift](https://spacelift.io/blog/what-is-terrascan).
  - [lib.meta.getExe @ Nixpkgs Reference Manual](https://nixos.org/manual/nixpkgs/stable/#function-library-lib.meta.getExe).
*/
{
  config,
  lib,
  pkgs,
  recipes-lib,
  ...
}:
let
  inherit (lib.modules) mkIf mkDefault;
  inherit (recipes-lib.modules) mkToolOptions;
  inherit (lib.attrsets) optionalAttrs;

  terraformCfg = config.biapy-recipes.terraform;
  cfg = terraformCfg.terrascan;

  inherit (pkgs) terrascan;
  terrascanCommand = lib.meta.getExe terrascan;
in
{
  options.biapy-recipes.terraform.terrascan = mkToolOptions terraformCfg "terrascan";

  config = mkIf cfg.enable {
    # https://devenv.sh/packages/
    packages = [ terrascan ];

    # https://devenv.sh/git-hooks/
    git-hooks.hooks = optionalAttrs cfg.git-hooks {
      terrascan = {
        enable = mkDefault false;
        name = "Terrascan";
        package = terrascan;
        pass_filenames = false;
        entry = ''${terrascanCommand} "scan"'';
      };
    };

    # https://devenv.sh/tasks/
    tasks = optionalAttrs cfg.tasks {
      "ci:lint:tf:terrascan" = {
        description = "ğŸ” Lint ğŸ—ï¸Infrastructure as Code with terrascan";
        exec = ''
          cd "''${DEVENV_ROOT}"
          ${terrascanCommand} 'scan' --iac-type 'terraform'
        '';
      };
    };

    biapy.go-task.taskfile.tasks = optionalAttrs cfg.go-task {
      "ci:lint:tf:terrascan" = {
        aliases = [ "terrascan" ];
        desc = "ğŸ” Lint ğŸ—ï¸Infrastructure as Code with terrascan";
        cmds = [ ''terrascan 'scan' --iac-type "terraform"'' ];
        requires.vars = [ "DEVENV_ROOT" ];
      };
    };
  };
}
